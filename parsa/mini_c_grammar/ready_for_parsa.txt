Function -> Type identifier ( ArgList ) CompoundStmt

ArgList -> Arg ArgList2

ArgList2 -> , Arg ArgList2
ArgList2 -> epsilon

Arg -> Type identifier
Declaration -> Type IdentList ;

Type -> int
Type -> float

IdentList -> identifier IdentList2

IdentList2 -> , identifier IdentList2
IdentList2 -> epsilon

Stmt -> ForStmt
Stmt -> WhileStmt
Stmt -> Expr ;
Stmt -> IfStmt
Stmt -> CompoundStmt
Stmt -> Declaration
Stmt -> ;

ForStmt -> for ( Expr ; OptExpr ; OptExpr ) Stmt

OptExpr -> Expr
OptExpr -> epsilon

WhileStmt -> while ( Expr ) Stmt
IfStmt -> if ( Expr ) Stmt ElsePart

ElsePart -> else Stmt
ElsePart -> epsilon

CompoundStmt -> { StmtList }

StmtList -> Stmt StmtList
StmtList -> epsilon

Expr -> identifier = Expr
Expr -> Rvalue

Rvalue -> Mag Rvalue2

Rvalue2 -> Compare Mag Rvalue2
Rvalue2 -> epsilon

Compare -> == 
Compare -> < 
Compare -> > 
Compare -> <= 
Compare -> >= 
Compare -> !=

Mag -> Term Mag2

Mag2 -> + Term Mag2
Mag2 -> - Term Mag2
Mag2 -> epsilon

Term -> Factor Term2

Term2 -> * Factor Term2
Term2 -> / Factor Term2
Term2 -> epsilon

Factor -> ( Expr )
Factor -> - Factor
Factor -> + Factor
Factor -> identifier
Factor -> number